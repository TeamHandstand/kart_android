buildscript {
    ext.android_support_version = '25.3.1'
    ext.android_test_runner_version = '0.5'
    ext.autovalue_version = '1.4.1'
    ext.autovalue_gson_version = '0.4.6'
    ext.crashlytics_version = '2.6.7'
    ext.espresso_version = '2.2.2'
    ext.glide_version = '3.8.0'
    ext.glide_okhttp3_integration_version = '1.5.0@aar'
    ext.gson_version = '2.8.0'
    ext.google_maps_version = '10.2.6'
    ext.junit_version = '4.12'
    ext.kotlin_version = '1.1.2-3'
    ext.mockito_version = '2.8.9'
    ext.okhttp3_version = '3.8.0'
    ext.powermock_version = '1.7.0RC4'
    ext.retrofit_version = '2.3.0'
    ext.rxandroid_version = '1.2.1'
    ext.sqlbrite_version = '1.1.1'
    ext.sqldelight_version = '0.6.1'

    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.squareup.sqldelight:gradle-plugin:$sqldelight_version"
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "us.handstand.kartwheel"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testFunctionalTest true
    }
    buildTypes {
        debug {
            // 10.0.2.2 is the address that the android emulator uses to access the host machine
            buildConfigField "String", "SERVER", "\"http://10.0.2.2:3000\""
        }

        release {
            buildConfigField "String", "SERVER", "\"https://www.handstandwith.us/kartwheel\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:$android_support_version"
    compile "com.android.support:design:$android_support_version"
    compile("com.crashlytics.sdk.android:crashlytics:$crashlytics_version@aar") { transitive = true; }
    compile "com.github.bumptech.glide:glide:$glide_version"
    compile "com.github.bumptech.glide:okhttp3-integration:$glide_okhttp3_integration_version"
    compile "com.google.android.gms:play-services-maps:$google_maps_version"
    compile "com.google.code.gson:gson:$gson_version"
    compile "com.squareup.okhttp3:okhttp:$okhttp3_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.sqlbrite:sqlbrite:$sqlbrite_version"
    compile "io.reactivex:rxandroid:$rxandroid_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    kapt "com.google.auto.value:auto-value:$autovalue_version"
    kapt "com.ryanharter.auto.value:auto-value-gson:$autovalue_gson_version"

    provided "com.google.auto.value:auto-value:$autovalue_version"
    provided "com.ryanharter.auto.value:auto-value-gson:$autovalue_gson_version"

    androidTestCompile "com.android.support:support-annotations:$android_support_version"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$espresso_version"
    androidTestCompile "com.android.support.test:runner:$android_test_runner_version"

    testCompile "com.squareup.okhttp3:mockwebserver:$okhttp3_version"
    testCompile "org.mockito:mockito-core:$mockito_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.powermock:powermock-api-mockito2:$powermock_version"
    testCompile "org.powermock:powermock-module-junit4:$powermock_version"
    testCompile "junit:junit:$junit_version"
}

